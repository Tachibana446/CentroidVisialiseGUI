// Shift JIS
/*************************************************
人間のジョイントの各パーツ・構造を推定するスクリプト
**************************************************/

global proc createWindow() {
    if((`window -ex centroidGuiWindow`) == true) deleteUI centroidGuiWindow;
    string $win = `window centroidGuiWindow`;

    scrollLayout;
    
    text -l "① ルート（腰）のジョイントを指定してください。";

    rowLayout -nc 3;
    text -l "ルートジョイント";
    textField rootJointTextField;
    button -l "SET" -c "setCurrentObject(\"rootJointTextField\")";
    setParent ..;

    text -l "② 各ジョイントを推定します。";
    button -l "ジョイント構造の推定" -c "SearchBones_ButtonClick()";
    
    separator -st "doubleDash" -hr true -w 300 -h 15;
    


    rowColumnLayout -nc 3;

    text -l "胴";
    rowLayout -nc 2;
    textField chestJointTextField;
    button -l "SET" -c "setCurrentObject(\"chestJointTextField\")";
    setParent ..;
    text -l "";

    text -l "" -h 15;
    text -l "左";
    text -l "右";


    text -l "上腕";
    rowLayout -nc 2;
    textField upperArmL_textfield;
    button -l "SET" -c "setCurrentObject(\"upperArmL_textfield\")";
    setParent ..;

    rowLayout -nc 2;
    textField upperArmR_textfield;
    button -l "SET" -c "setCurrentObject(\"upperArmR_textfield\")";
    setParent ..;

    text -l "前腕";
    
    rowLayout -nc 2;
    textField foreArmL_textField;
    button -l "SET" -c "setCurrentObject(\"foreArmL_textField\")";
    setParent ..;
    rowLayout -nc 2;
    textField foreArmR_textField;
    button -l "SET" -c "setCurrentObject(\"foreArmR_textField\")";
    setParent ..;
    
    text -l "手";
    rowLayout -nc 2;
    textField handL_textField;
    button -l "SET" -c "setCurrentObject(\"handL_textField\")";
    setParent ..; 
    rowLayout -nc 2;
    textField handR_textField;
    button -l "SET" -c "setCurrentObject(\"handR_textField\")";
    setParent ..;

    text -l "大腿";
    rowLayout -nc 2;
    textField upperLegL_textField;
    button -l "SET" -c "setCurrentObject(\"upperLegL_textField\")";
    setParent ..;
    rowLayout -nc 2;
    textField upperLegR_textField;
    button -l "SET" -c "setCurrentObject(\"upperLegR_textField\")";
    setParent ..;

    text -l "下腿（膝）";
    rowLayout -nc 2;
    textField underLegL_textField;
    button -l "SET" -c "setCurrentObject(\"underLegL_textField\")";
    setParent ..;
    rowLayout -nc 2;
    textField underLegR_textField;
    button -l "SET" -c "setCurrentObject(\"underLegR_textField\")";
    setParent ..;
    
    text -l "足";
    rowLayout -nc 2;
    textField legL_textField;
    button -l "SET" -c "setCurrentObject(\"legL_textField\")";
    setParent ..;
    rowLayout -nc 2;
    textField legR_textField;
    button -l "SET" -c "setCurrentObject(\"legR_textField\")";
    setParent ..;

    setParent ..;
    
    separator -st "doubleDash" -hr true -w 300;
    
    text -l "③ 人間の体重を入力";
    rowLayout -nc 2;
    text -l "総体重";
    floatField -v 100 -min 1 allWeightField;
    setParent ..;

    text -l "④ 推定したジョイントが正しければ実行してください";
    button -l "重心の作成" -c "tcbn_locatorSettingWindow()";

    showWindow $win;
}

proc setCurrentObject(string $fieldName){
    $current = `ls -sl -head 1`;
    textField -e -tx $current $fieldName;
}

proc SearchBones_ButtonClick(){
    $root = `textField -q -tx rootJointTextField`;
    SearchBones($root);
}

proc SearchBones(string $root){
    $children = `listRelatives -path -type "joint" $root`;
   
    $chest = "";
    $chestY = -9999;
    for ($child in $children)
    {
        string $_chest = GetChestRecursive($child);
        if($_chest == "")
            continue;
        $_pos = `xform -q -ws -t $_chest`;
        if($chestY == -9999 || $_pos[1] > $chestY){
            $chest = $_chest;
            $chestY = $_pos[1];
        }
    }
    if($chest != "")
    {
        textField -e -tx $chest chestJointTextField;
        // 上腕の取得
        SearchUpperArms($chest);
        $waist = GetWaistRecursive($chest, $root);
        SearchUpperLegJoint($root, $waist);
    }
}
        
// 上腕の取得
proc SearchUpperArms(string $chest){
    $upperChildren = `listRelatives -path -type "joint" $chest`;
    int $lengthList[];
    $i = 0;
    for ($uc in $upperChildren)
    {
        $lengthList[$i] = GetDecendantsCount($uc, 0);
        $i += 1;
    }
    $sorted = sort($lengthList);
    string $upperArm1 = "";
    string $upperArm2;
    $i = 0;
    // 最も子孫が長い2つを腕と認定
    for ($uc in $upperChildren)
    {   
        if($upperArm1 == "" && $lengthList[$i] == $sorted[size($sorted) - 1])
            $upperArm1 = $uc;
        else if($upperArm1 != "" && $lengthList[$i] == $sorted[size($sorted) - 2])
        {
            $upperArm2 = $uc;
            break;
        }
        $i += 1;
    }
    $pos1 = `xform -q -ws -t $upperArm1`;
    $pos2 = `xform -q -ws -t $upperArm2`;
    // （前から見て）左にある方が右腕
    string $upperArmL;
    string $upperArmR;
    if($pos1[0] < $pos2[0]){
        $upperArmR = $upperArm1;
        $upperArmL = $upperArm2;
    }else{
        $upperArmL = $upperArm1;
        $upperArmR = $upperArm2;
    }
    textField -e -tx $upperArmR upperArmR_textfield;
    textField -e -tx $upperArmL upperArmL_textfield;
    SearchHandJoint($upperArmL, $upperArmR);
}

// 前腕と手を取得
proc SearchHandJoint(string $upperArmL, string $upperArmR){
    $handL = GetChestRecursive($upperArmL);
    if($handL != ""){
        $foreArmL = `listRelatives -path -p $handL`;
        textField -e -tx $foreArmL[0] foreArmL_textField;
    }
    textField -e -tx $handL handL_textField;
    $handR = GetChestRecursive($upperArmR);
    if($handR != ""){
        $foreArmR = `listRelatives -path -p $handR`;
        textField -e -tx $foreArmR[0] foreArmR_textField;
    }
    textField -e -tx $handR handR_textField;
}

// 上肢取得
// input $waist: ルート直下の上半身に続くノード
proc SearchUpperLegJoint(string $root, string $waist){
    $children = `listRelatives -path -type "joint" $root`;
    // 上半身は除く
    $children = stringArrayRemove({ $waist }, $children );
    // 子孫の長さをカウント
    int $lengthList[];
    for ($i=0; $i < size($children); $i++)
    {
        $lengthList[$i] = GetDecendantsCount($children[$i], 0);
    }
    $sorted = sort($lengthList);
    string $upperLeg1 = "";
    string $upperLeg2;
    // 最も子孫が長い2つを足とする
    for ($i=0; $i < size($children); $i++)
    {
        if($upperLeg1 == "" && $lengthList[$i] == $sorted[size($sorted) - 1])
            $upperLeg1 = $children[$i];
        else if($upperLeg1 != "" &&  $lengthList[$i] == $sorted[size($sorted) - 2]) 
        {
            $upperLeg2 = $children[$i];
            break;
        }    
    }
    // 前から見て右にある方が左足
    string $upperLegL, $upperLegR;
    $pos1 = `xform -q -ws -t $upperLeg1`;
    $pos2 = `xform -q -ws -t $upperLeg2`;
    if($pos1[0] > $pos2[0]){
        $upperLegL = $upperLeg1;
        $upperLegR = $upperLeg2;
    }else{
        $upperLegL = $upperLeg2;
        $upperLegR = $upperLeg1;
    }
    textField -e -tx $upperLegL upperLegL_textField;
    textField -e -tx $upperLegR upperLegR_textField;
    GetLegJoint($upperLegL, "legL_textField", "underLegL_textField");
    GetLegJoint($upperLegR, "legR_textField", "underLegR_textField");
}

proc GetLegJoint(string $upperLeg, string $legTextField, string $underLegTextField){
    // 派生するものがあればそのジョイントを足とする
    $leg = GetChestRecursive($upperLeg);
    if($leg != ""){
        $underLeg = `listRelatives -path -p $leg`;
        textField -e -tx $underLeg $underLegTextField;
    }
    // もし派生ジョイントがない＝一直線のボーン構造なら足の根元の次を膝・その次を足としておく
    else{
        $decendents = `listRelatives -path -ad -type "joint" $upperLeg`;
        $leg = $decendents[size($decendents) - 2];
        $underLeg = $decendents[size($decendents) - 1];
        textField -e -tx $underLeg $underLegTextField;
    }
    textField -e -tx $leg $legTextField;
}

// 胸から下、ルートの付け根の腰を取得する
proc string GetWaistRecursive(string $chest, string $root){
    $parent = `listRelatives -path -p $chest`;
    if(size($parent) == 0)
        return "";
    if($parent[0] == $root)
        return $chest;
    else
        return GetWaistRecursive($parent[0], $root);
}

// 胸から下、ルートまでをリストとして取得する
proc string[] GetWaistDecendents(string $chest, string $root){
    string $result[];
    if($chest == "" || $root == "") return $result;
    int $i = 0;
    $now = $chest;
    
    while ($now != $root)
    {
        $result[$i] = $now;
        $parent = `listRelatives -path -p $now`;
        if(size($parent) == 0)
            break;
        $now = $parent[0];
        $i++;
        if($i > 100)
            break;
    }
    if($now == $root)
        $result[size($result)] = $root; // ルートも含める
    
    return $result;
}

// 再帰して複数の子を持つジョイントを探す
proc string GetChestRecursive(string $joint){
    $children = `listRelatives -path -type "joint" $joint`;
    $count = `size $children`;
    if($count == 0 )
        return "";
    else if($count >= 2)
        return $joint;
    else
    {
        return GetChestRecursive($children[0]);
    }
}

// 再帰して子孫の長さ(何代まで続いたか)を取得
proc int GetDecendantsCount(string $joint, int $count){
    $children = `listRelatives -path -type "joint" $joint`;
    $count = `size $children`;
    if($count == 0)
        return $count;
    // 各兄弟の中で最も子孫が長いもののカウントを返す
    $max = $count;
    for ($c in $children)
    {
        $num = GetDecendantsCount($c, $count + 1);
        if($num > $max)
            $max = $num;  
    }
    return $max;
}

/*******************************************************
** ロケータを配置するウィンドウ                         **
********************************************************/
proc tcbn_locatorSettingWindow(){
    print("locwin\n");
    if((`window -ex tcbn_locatorSettingWindow`) == true) deleteUI tcbn_locatorSettingWindow;
    string $win = `window tcbn_locatorSettingWindow`;

    scrollLayout;
    button -l "ロケータの生成";

    $group = `group -em -w -n centroidLoctorsGroup`;

    $root = `textField -q -tx rootJointTextField`;
    $chest = `textField -q -tx chestJointTextField`;
    $bodys = GetWaistDecendents($chest, $root);
    
    print "bodys:\n";
    print $bodys;
    print "\n ";


    rowColumnLayout -nc 3 locatorsLayout;

    text -l "部位";
    text -l "ロケータ";
    text -l "重さ";

    $upperCentroids = `createNode compositeCentroidNode -n "upperCentroidsNode#"`;
    $j = 0;

    $weight = `floatField -q -v allWeightField`;
    for ($i=0; $i<(size($bodys)); $i++)
    {
        text -l ("胴" + $i) ("body" + $i);
        $loc = createLocatorAtJoint(("body"+$i+"_locator#"), $bodys[$i], $group);
        textField -tx $loc ("body" + $i + "locator_textField");
        floatField -v ($weight / size($bodys) * 0.43) ("body" + $i + "locator_weightField");
    
        parentConstraint -mo $bodys[$i] $loc;
        connectAttr -f  ($loc + "Shape.worldPosition[0]") ($upperCentroids + ".inputArray[" + $j + "].position");
        setAttr  ($upperCentroids + ".inputArray[" + $j + "].weight") ($weight / size($bodys) * 0.43) ;
        $j++;
    }

    

    text -l "上腕左";
    $joint = `textField -q -tx upperArmL_textfield`;
    $loc2 = createLocatorAtJoint("upperArm_L_locator#", $joint, $group);
    textField -tx $loc2 ("upperArm_L_locaotor_textField");
    floatField -v ($weight * 0.035) upperArm_L_locator_weightField;
    parentConstraint -mo $joint $loc2;
    connectAttr -f  ($loc2 + "Shape.worldPosition[0]") ($upperCentroids +".inputArray[" + $j + "].position");
    setAttr ($upperCentroids +".inputArray[" + $j + "].weight") ($weight * 0.035);
    $j++;
    text -l "上腕右";
    $joint = `textField -q -tx upperArmR_textfield`;
    $loc2 = createLocatorAtJoint("upperArm_R_locator#", $joint, $group);
    textField -tx $loc2 ("upperArm_R_locaotor_textField");
    floatField -v ($weight * 0.035) upperArm_R_locator_weightField;
    parentConstraint -mo $joint $loc2;
    connectAttr -f  ($loc2 + "Shape.worldPosition[0]") ($upperCentroids +".inputArray[" + $j + "].position");
    setAttr ($upperCentroids +".inputArray[" + $j + "].weight") ($weight * 0.035);
    $j++;
    text -l "左前腕";
    $joint = `textField -q -tx foreArmL_textField`;
    $loc2 = createLocatorAtJoint("foreArm_L_locator#", $joint, $group);
    textField -tx $loc2 ("foreArm_L_locator_textField");
    floatField -v ($weight * 0.023) foreArm_L_locator_weightField;
    parentConstraint -mo $joint $loc2;
    connectAttr -f  ($loc2 + "Shape.worldPosition[0]") ($upperCentroids +".inputArray[" + $j + "].position");
    setAttr ($upperCentroids +".inputArray[" + $j + "].weight") ($weight * 0.023);
    $j++;
    text -l "右前腕";
    $joint = `textField -q -tx foreArmR_textField`;
    $loc2 = createLocatorAtJoint("foreArm_R_locator#", $joint, $group);
    textField -tx $loc2 ("foreArm_R_locaotor_textField");
    floatField -v ($weight * 0.023) foreArm_R_locator_weightField;
    parentConstraint -mo $joint $loc2;
    connectAttr -f  ($loc2 + "Shape.worldPosition[0]") ($upperCentroids +".inputArray[" + $j + "].position");
    setAttr ($upperCentroids +".inputArray[" + $j + "].weight") ($weight * 0.023);
    $j++;
    text -l "左手";
    $joint = `textField -q -tx handL_textField`;
    $loc2 = createLocatorAtJoint("hand_L_locator#", $joint, $group);
    textField -tx $loc2 ("hand_L_locator_textField");
    floatField -v ($weight * 0.008) hand_L_locator_weightField;
    parentConstraint -mo $joint $loc2;
    connectAttr -f  ($loc2 + "Shape.worldPosition[0]") ($upperCentroids +".inputArray[" + $j + "].position");
    setAttr ($upperCentroids +".inputArray[" + $j + "].weight") ($weight * 0.008);
    $j++;
    text -l "右手";
    $joint = `textField -q -tx handR_textField`;
    $loc2 = createLocatorAtJoint("hand_R_locator#", $joint, $group);
    textField -tx $loc2 ("hand_R_locaotor_textField");
    floatField -v ($weight * 0.008) hand_R_locator_weightField;
    parentConstraint -mo $joint $loc2;
    connectAttr -f  ($loc2 + "Shape.worldPosition[0]") ($upperCentroids +".inputArray[" + $j + "].position");
    setAttr ($upperCentroids +".inputArray[" + $j + "].weight") ($weight * 0.008);
    $j++;

    $lowerCentroids = `createNode compositeCentroidNode -n "lowerCentroidNode#"`;
    $j = 0;
    text -l "左大腿";
    $joint = `textField -q -tx upperLegL_textField`;
    $loc2 = createLocatorAtJoint("upperLeg_L_locator#", $joint, $group);
    textField -tx $loc2 ("upperLeg_L_locaotor_textField");
    floatField -v ($weight * 0.116) upperLeg_L_locator_weightField;
    parentConstraint -mo $joint $loc2;
    connectAttr -f  ($loc2 + "Shape.worldPosition[0]") ($lowerCentroids +".inputArray[" + $j + "].position");
    setAttr ($lowerCentroids +".inputArray[" + $j + "].weight") ($weight * 0.116);
    $j++;
    text -l "右大腿";
    $joint = `textField -q -tx upperLegR_textField`;
    $loc2 = createLocatorAtJoint("upperLeg_R_locator#", $joint, $group);
    textField -tx $loc2 ("upperLeg_R_locaotor_textField");
    floatField -v ($weight * 0.116) upperLeg_R_locator_weightField;
    parentConstraint -mo $joint $loc2;
    connectAttr -f  ($loc2 + "Shape.worldPosition[0]") ($lowerCentroids +".inputArray[" + $j + "].position");
    setAttr ($lowerCentroids +".inputArray[" + $j + "].weight") ($weight * 0.116);
    $j++;
    text -l "左下腿";
    $joint = `textField -q -tx underLegL_textField`;
    $loc2 = createLocatorAtJoint("underLeg_L_locator#", $joint, $group);
    textField -tx $loc2 ("underLeg_L_locator_textField");
    floatField -v ($weight * 0.053) underLeg_L_locator_weightField;
    parentConstraint -mo $joint $loc2;
    connectAttr -f  ($loc2 + "Shape.worldPosition[0]") ($lowerCentroids +".inputArray[" + $j + "].position");
    setAttr ($lowerCentroids +".inputArray[" + $j + "].weight") ($weight * 0.053);
    $j++;
    text -l "右下腿";
    $joint = `textField -q -tx underLegR_textField`;
    $loc2 = createLocatorAtJoint("underLeg_R_locator#", $joint, $group);
    textField -tx $loc2 ("underLeg_R_locaotor_textField");
    floatField -v ($weight * 0.053) underLeg_R_locator_weightField;
    parentConstraint -mo $joint $loc2;
    connectAttr -f  ($loc2 + "Shape.worldPosition[0]") ($lowerCentroids +".inputArray[" + $j + "].position");
    setAttr ($lowerCentroids +".inputArray[" + $j + "].weight") ($weight * 0.053);
    $j++;
    text -l "左脚";
    $joint = `textField -q -tx legL_textField`;
    $loc2 = createLocatorAtJoint("leg_L_locator#", $joint, $group);
    textField -tx $loc2 ("leg_L_locaotor_textField");
    floatField -v ($weight * 0.018) leg_L_locator_weightField;
    parentConstraint -mo $joint $loc2;
    connectAttr -f  ($loc2 + "Shape.worldPosition[0]") ($lowerCentroids +".inputArray[" + $j + "].position");
    setAttr ($lowerCentroids +".inputArray[" + $j + "].weight") ($weight * 0.018);
    $j++;
    text -l "右足";
    $joint = `textField -q -tx legR_textField`;
    $loc2 = createLocatorAtJoint("leg_R_locator#", $joint, $group);
    textField -tx $loc2 ("leg_R_locaotor_textField");
    floatField -v ($weight * 0.018) leg_R_locator_weightField;
    parentConstraint -mo $joint $loc2;
    connectAttr -f  ($loc2 + "Shape.worldPosition[0]") ($lowerCentroids +".inputArray[" + $j + "].position");
    setAttr ($lowerCentroids +".inputArray[" + $j + "].weight") ($weight * 0.018);
    
    $allCentroid = `createNode compositeCentroidNode -n "fullBodyCentroidNode#"`;
    connectAttr -f ($upperCentroids + ".outputPosition") ($allCentroid  + ".inputArray[0].position");
    connectAttr -f ($upperCentroids + ".outputWeight") ($allCentroid  + ".inputArray[0].weight");
    connectAttr -f ($lowerCentroids + ".outputPosition") ($allCentroid  + ".inputArray[1].position");
    connectAttr -f ($lowerCentroids + ".outputWeight") ($allCentroid  + ".inputArray[1].weight");

    
    $line = `curve -d 1 -p 0 0 0 -p 0 0 0 -k 0 -k 1 `;
    parent $line $group;
    $l1 = `pointCurveConstraint -ch true -rpo true -w 1 ($line + ".u[0]") `;
    $l2 = `pointCurveConstraint -ch true -rpo true -w 1 ($line + ".u[1]") `;
    parent $l1[0] $group;
    parent $l2[0] $group;
    connectAttr -f ($allCentroid + ".outputPosition") ($l1[0] + ".translate");
    connectAttr -f ($allCentroid + ".outputPosition0") ($l2[0] + ".tx");
    connectAttr -f ($allCentroid + ".outputPosition2") ($l2[0] + ".tz");
    $line = `rename $line "centroidVisualizedCurve#"`;

    setParent ..;
    
    showWindow $win;
}


/// ジョイントの位置にロケータを作成し、指定グループの子にする
proc string createLocatorAtJoint(string $name, string $joint,string $group){
    $loc = `spaceLocator -p 0 0 0 -n $name`;
    parent -r $loc $joint;
    parent $loc $group;
    return $loc[0];
}   


createWindow();